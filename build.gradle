group 'example.bidrectional.grpc'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile "io.grpc:grpc-all:${grpcVersion}"
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/proto'
        }
        java {
            // include self written and generated code
            srcDirs "$projectDir/src/gen/main/java", "$projectDir/src/gen/main/grpc"
        }
    }
    // remove the test configuration - at least in your example you don't have a special test proto file
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/gen"
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // To generate deprecated interfaces and static bindService method,
                // turn the enable_deprecated option to true below:
                option 'enable_deprecated=false'
            }
        }
    }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}